export ZSH=$HOME/.dotfiles
export PROJECTS=~/Code

typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#**/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion file after autocomplete loads
for file in ${(M)config_files:#**/completion.zsh}
do
  source $file
done

unset config_files

# use .secretrc for SUPER SECRET or OS specific things
if [[ -a ~/.secretrc ]]
then
  source ~/.secretrc
fi

export NVM_DIR="$HOME/.nvm"
[ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh"  # This loads nvm
[ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion"  # This loads nvm bash_completion
